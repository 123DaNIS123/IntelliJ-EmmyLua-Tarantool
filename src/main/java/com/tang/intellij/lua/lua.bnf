/*
 * Copyright (c) 2017. tangzx(love.tangzx@qq.com)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

{
  parserClass="com.tang.intellij.lua.parser.LuaParser"

  implements="com.tang.intellij.lua.psi.LuaPsiElement"
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Lua"
  psiImplClassSuffix="Impl"
  psiPackage="com.tang.intellij.lua.psi"
  psiImplPackage="com.tang.intellij.lua.psi.impl"
  psiImplUtilClass="com.tang.intellij.lua.psi.LuaPsiImplUtil"

  parserUtilClass="com.tang.intellij.lua.psi.LuaParserUtil"
  elementTypeHolderClass="com.tang.intellij.lua.psi.LuaTypes"
  elementTypeClass="com.tang.intellij.lua.psi.LuaElementType"
  elementTypeFactory="com.tang.intellij.lua.lang.LuaParserDefinition.createType"
  tokenTypeClass="com.tang.intellij.lua.psi.LuaTokenType"
  tokenTypeFactory="com.tang.intellij.lua.lang.LuaParserDefinition.createToken"
  parserImports = "static com.intellij.lang.WhitespacesBinders.*"

  tokens = [
        /* Keywords */
        AND = "and"
        BREAK = "break"
        DO = "do"
        ELSE = "else"
        ELSEIF = "elseif"
        END = "end"
        FALSE = "false"
        FOR = "for"
        FUNCTION = "function"
        IF = "if"
        IN = "in"
        LOCAL = "local"
        NIL = "nil"
        NOT = "not"
        OR = "or"
        REPEAT = "repeat"
        RETURN = "return"
        THEN = "then"
        TRUE = "true"
        UNTIL = "until"
        WHILE = "while"
        REGION = "REGION"
        ENDREGION = "ENDREGION"

        SHEBANG = "#!"
        ELLIPSIS = "..."
        CONCAT = ".."
        EQ = "=="
        GE = ">="
        LE = "<="
        NE = "~="
        MINUS = "-"
        PLUS = "+"
        MULT = "*"
        MOD = "%"
        DIV = "/"
        ASSIGN = "="
        GT = ">"
        LT = "<"
        LPAREN = "("
        RPAREN = ")"
        LBRACK = "["
        RBRACK = "]"
        LCURLY = "{"
        RCURLY = "}"
        GETN = "#"
        COMMA = ","
        SEMI = ";"
        COLON = ":"
        DOT = "."
        EXP = "^"

        SHORT_COMMENT="SHORT_COMMENT"
        DOC_COMMENT="DOC_COMMENT"
        BLOCK_COMMENT="BLOCK_COMMENT"
        ID="regexp:[A-Za-z_][A-Za-z0-9_]*"
        NUMBER="NUMBER"
        STRING = "STRING"

        //lua 5.3
        DOUBLE_COLON = "::"
        GOTO = "goto"
        BIT_OR = "|"
        BIT_AND = "&"
        BIT_LTLT = "<<"
        BIT_RTRT = ">>"
        BIT_TILDE = "~"
        DOUBLE_DIV = "//"
  ]

  implements("tableConstructor") = "com.tang.intellij.lua.psi.LuaIndentRange"
  hooks("forAStat|forBStat") = [leftBinder="MY_LEFT_COMMENT_BINDER"]
  implements("forAStat|forBStat") = "com.tang.intellij.lua.psi.LuaIndentRange, com.tang.intellij.lua.psi.LuaParametersOwner, com.tang.intellij.lua.psi.LuaCommentOwner"
  hooks("localDef|assignStat") = [leftBinder="MY_LEFT_COMMENT_BINDER"]
  implements("localDef|assignStat") = "com.tang.intellij.lua.psi.LuaDeclaration"
  hooks("globalFuncDef|localFuncDef|classMethodDef") = [leftBinder="MY_LEFT_COMMENT_BINDER"]
  hooks("fieldList") = [leftBinder="GREEDY_LEFT_BINDER" rightBinder="GREEDY_RIGHT_BINDER"]
  hooks("defStat") = [leftBinder="MY_LEFT_COMMENT_BINDER"]
  implements("expr|funcCall") = "com.tang.intellij.lua.psi.LuaExpression"
  implements("doStat|whileStat|repeatStat|ifStat") = "com.tang.intellij.lua.psi.LuaIndentRange"
  implements("funcBody") = "com.tang.intellij.lua.psi.LuaIndentRange"
  extends(".*Expr") = expr
  extends(".*Stat") = statement
}

luaFile ::= stat_semi*

private stat_semi ::= stat_impl ';'?
block ::= stat_semi* (lastStat ';'?)?
private lazy_block ::= <<lazyBlock>>

fake statement ::= ()
fake exprStat ::= expr
private stat_impl ::=
    callStat |
    assignStat |
    doStat |
    whileStat |
    repeatStat |
    ifStat |
    forAStat |
    forBStat |
    defStat |
    lastStat |
    labelStat |
    gotoStat |
    uncompletedStat {
        recoverWhile = stat_recover
    }
private stat_recover ::= !(ID
    | ',' | ';'
    | 'local' | 'do' | 'while' | 'repeat' | 'function' | 'if' | 'for' | 'return' | break
    | nil | true | false | STRING | NUMBER | '::' | 'goto'
    | unaryOp)

assignStat ::= varList '=' exprList { pin = 2 methods = [getComment assign = 'ASSIGN'] }
doStat ::= 'do' lazy_block 'end' { pin = 1 }
whileStat ::= 'while' expr 'do' lazy_block 'end' { pin = 1 }
repeatStat ::= 'repeat' lazy_block 'until' expr { pin = 1 }
ifStat ::= 'if' expr 'then' lazy_block ('elseif' expr 'then' lazy_block)* ('else' lazy_block)? 'end' { pin = 1 }
forAStat ::= 'for' paramNameDef '=' expr ',' expr (',' expr)? 'do' lazy_block 'end' { pin = 3 methods = [getComment getParamNameDefList] }
forBStat ::= 'for' parList 'in' exprList 'do' lazy_block 'end' { pin = 1 methods = [getComment] }
labelStat ::= '::' ID '::' { pin = 1 }
gotoStat ::= 'goto' ID { pin = 1 }
defStat ::= classMethodDef | globalFuncDef | localFuncDef | localDef
callStat ::= funcCallExpr { extends = exprStat }
uncompletedStat ::= expr { extends = exprStat }

globalFuncDef ::= 'function' ID funcBody {
    pin = 1
    implements = [
        "com.tang.intellij.lua.psi.LuaFuncBodyOwner"
        "com.tang.intellij.lua.psi.LuaDeclaration"
        "com.intellij.psi.PsiNameIdentifierOwner"
    ]
    methods = [
        getComment
        getPresentation
        getParamNameDefList
        getNameIdentifier
        setName
        getName
        getTextOffset
        toString
        guessReturnTypeSet
        getParams
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaGlobalFuncStub"
}

private checkFuncPrefix ::= ID ('.' | ':') ID

left funcPrefixRef ::= '.' <<repeat checkFuncPrefix 1>> ID { extends = indexExpr elementType = indexExpr }
classMethodName ::=  nameExpr (funcPrefixRef)* (('.' ID) | (':' ID)) {
    pin = 3
    methods = [
        dot = 'DOT'
        colon = 'COLON'
    ]
}
classMethodDef ::= 'function' classMethodName funcBody {
    pin = 2
    implements = [
        "com.tang.intellij.lua.psi.LuaFuncBodyOwner"
        "com.tang.intellij.lua.psi.LuaDeclaration"
        "com.tang.intellij.lua.psi.LuaClassMember"
        "com.intellij.psi.PsiNameIdentifierOwner"
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaClassMethodStub"
    methods = [
        getComment
        getClassType
        getParamNameDefList
        getNameIdentifier
        setName
        getName
        getTextOffset
        toString
        guessReturnTypeSet
        getParams
        isStatic
        getPresentation
    ]
}

localFuncDef ::= 'local' 'function' ID funcBody {
    pin = 2
    implements = [
        "com.tang.intellij.lua.psi.LuaFuncBodyOwner"
        "com.tang.intellij.lua.psi.LuaDeclaration"
        "com.intellij.psi.PsiNameIdentifierOwner"
    ]
    methods = [
        getComment
        getParamNameDefList
        getNameIdentifier
        setName
        getName
        getTextOffset
        getUseScope
        guessReturnTypeSet
        getParams
    ]
}
localDef ::= 'local' nameList ('=' exprList)? {
    pin = 1
    methods = [getComment assign = 'ASSIGN']
}

returnStat ::= return exprList?
breakStat ::= break
private lastStat ::= returnStat | breakStat

varList ::= var (',' var)*

nameList ::= nameDef (',' nameDef)*

exprList ::= (expr ',')* expr { methods = [guessTypeAt] }

literalExpr ::= nil | false | true | NUMBER | STRING | "..."

valueExpr ::= literalExpr | closureFuncDef | tableConstructor | var | parenExpr

// 最终产生 call expr | index expr | nameRef
var ::= callOrIndexExpr | nameExpr {
    methods = [
        guessType
    ]
}

expr ::=
    orExpr |
    andExpr |
    conditionalExpr |
    bitOrExpr |
    bitTildeExpr |
    bitAndExpr |
    bitMoveExpr |
    concatExpr |
    addExpr |
    mulExpr |
    unaryExpr |
    expExpr |
    valueExpr { mixin = "com.tang.intellij.lua.psi.impl.LuaExpressionImpl" }

parenExpr ::= '(' expr ')' { pin = 1 }
unaryExpr ::= unaryOp expr { pin = 1 }

fake binaryExpr ::= expr binaryOp expr
orExpr ::= expr 'or' expr { extends = binaryExpr }
andExpr ::= expr 'and' expr { extends = binaryExpr }
conditionalExpr ::= expr ('<'|'>'|'<='|'>='|'~='|'==') expr { extends = binaryExpr }
bitOrExpr ::= expr '|' expr { extends = binaryExpr }
bitTildeExpr ::= expr '~' expr { extends = binaryExpr }
bitAndExpr ::= expr '&' expr { extends = binaryExpr }
bitMoveExpr ::= expr ('<<'|'>>') expr { extends = binaryExpr }
concatExpr ::= expr '..' expr { extends = binaryExpr }
addExpr ::= expr ('+'|'-') expr { extends = binaryExpr }
mulExpr ::= expr ('*'|'/'|'//'|'%') expr { extends = binaryExpr }
expExpr ::= expr '^' expr { extends = binaryExpr }

left indexExpr ::= '[' expr ']' | '.' ID | ':' ID {
    pin(".*") = 1
    methods = [
        getNameIdentifier
        setName
        getName
        getTextOffset
        getPresentation
        toString

        guessPrefixType
        guessValueType

        dot = 'DOT'
        colon = 'COLON'
    ]
    implements = [
        "com.intellij.psi.PsiNameIdentifierOwner"
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaIndexStub"
    mixin = "com.tang.intellij.lua.psi.impl.LuaIndexExpressionImpl"
}

arg_expr_list ::= (expr ',')* (expr |& ')') { extends = exprList elementType = exprList }
args ::=  '(' (arg_expr_list)? ')' | tableConstructor | STRING { pin(".*") = 1 }
left callExpr ::= args {
    methods = [
        guessPrefixType
        resolveFuncBodyOwner
        getFirstStringArg

        isStaticMethodCall
        isMethodCall
        isFunctionCall
    ]
}

private suffixExpr ::= indexExpr | callExpr
private prefixExpr ::= parenExpr | nameExpr
// 最终产生 callExpr | indexExpr
private callOrIndexExpr ::= prefixExpr suffixExpr+
// callExpr
private funcCallExpr ::= callOrIndexExpr <<checkType 'CALL_EXPR'>>

closureFuncDef ::= 'function' funcBody {
    pin = 2
    implements = "com.tang.intellij.lua.psi.LuaFuncBodyOwner"
    methods = [getParamNameDefList guessReturnTypeSet getParams]
}

funcBody ::= '(' (parList)? ')' lazy_block? 'end' { pin = 3 }

private parList ::= paramNameDef (',' paramNameDef)* (',' '...')? | '...' { recoverWhile = parList_recover }
private parList_recover ::= !(')'|'in')

tableConstructor ::= '{' fieldList '}' {
    pin = 1
    methods = [findField toString]
    stubClass = "com.tang.intellij.lua.stubs.LuaTableStub"
}

fieldList ::= (tableField (tableFieldSep tableField)* (tableFieldSep)?)?

tableField ::= '[' expr ']' '=' expr | ID '=' expr | expr {
    methods = [
        getNameIdentifier
        setName
        getName
        getTextOffset

        toString

        getFieldName
        guessType
        nameDef = "tableFieldNameDef"
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaTableFieldStub"
    implements = [
        "com.tang.intellij.lua.psi.LuaClassField"
        "com.intellij.psi.PsiNameIdentifierOwner"
    ]
}

tableFieldSep ::= ',' | ';'

binaryOp ::= '+' | '-' | '*' | '/' | '^' | '%' | '..' |
    '<' | '<=' | '>' | '>=' | '==' | '~=' |
    'and' | 'or'
    // lua5.3
    | '|' | '&' | '>>' | '<<' | '~' | '//'

unaryOp ::= '-' | 'not' | '#'
    // lua5.3
    | '~'

fake name ::= ID {
    implements = [
        "com.tang.intellij.lua.psi.LuaNamedElement"
    ]
    methods = [setName getName]
}
nameDef ::= ID {
    implements = ["com.tang.intellij.lua.psi.LuaTypeGuessable" "com.intellij.psi.PsiNameIdentifierOwner"]
    methods = [guessType getNameIdentifier getUseScope]
    extends = name
}
nameExpr ::= ID {
    implements = ["com.tang.intellij.lua.psi.LuaPsiElement"
        "com.intellij.psi.PsiNameIdentifierOwner" ]
    methods = [
        setName
        getName
        getNameIdentifier
        getPresentation
        getReferences

        toString
    ]
    mixin = "com.tang.intellij.lua.psi.impl.LuaNameExpressionImpl"
    stubClass = "com.tang.intellij.lua.stubs.LuaNameStub"
}
paramNameDef ::= ID {
    extends = nameDef
}