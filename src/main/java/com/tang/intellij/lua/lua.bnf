{
  parserClass="com.tang.intellij.lua.parser.LuaParser"

  implements="com.tang.intellij.lua.psi.LuaPsiElement"
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Lua"
  psiImplClassSuffix="Impl"
  psiPackage="com.tang.intellij.lua.psi"
  psiImplPackage="com.tang.intellij.lua.psi.impl"
  psiImplUtilClass="com.tang.intellij.lua.psi.LuaPsiImplUtil"

  parserUtilClass="com.tang.intellij.lua.psi.LuaParserUtil"
  elementTypeHolderClass="com.tang.intellij.lua.psi.LuaTypes"
  elementTypeClass="com.tang.intellij.lua.psi.LuaElementType"
  elementTypeFactory="com.tang.intellij.lua.lang.LuaParserDefinition.createType"
  tokenTypeClass="com.tang.intellij.lua.psi.LuaTokenType"
  tokenTypeFactory="com.tang.intellij.lua.lang.LuaParserDefinition.createToken"

  tokens = [
        /* Keywords */
        AND = "and"
        BREAK = "break"
        DO = "do"
        ELSE = "else"
        ELSEIF = "elseif"
        END = "end"
        FALSE = "false"
        FOR = "for"
        FUNCTION = "function"
        IF = "if"
        IN = "in"
        LOCAL = "local"
        NIL = "nil"
        NOT = "not"
        OR = "or"
        REPEAT = "repeat"
        RETURN = "return"
        THEN = "then"
        TRUE = "true"
        UNTIL = "until"
        WHILE = "while"
        SELF = "self"

        SHEBANG = "#!"
        ELLIPSIS = "..."
        CONCAT = ".."
        EQ = "=="
        GE = ">="
        LE = "<="
        NE = "~="
        MINUS = "-"
        PLUS = "+"
        MULT = "*"
        MOD = "%"
        DIV = "/"
        ASSIGN = "="
        GT = ">"
        LT = "<"
        LPAREN = "("
        RPAREN = ")"
        LBRACK = "["
        RBRACK = "]"
        LCURLY = "{"
        RCURLY = "}"
        GETN = "#"
        COMMA = ","
        SEMI = ";"
        COLON = ":"
        DOT = "."
        EXP = "^"

        SHORT_COMMENT="SHORT_COMMENT"
        DOC_COMMENT="DOC_COMMENT"
        BLOCK_COMMENT="BLOCK_COMMENT"
        ID="regexp:[A-Za-z_][A-Za-z0-9_]*"
        NUMBER="NUMBER"
        STRING = "STRING"
  ]

  implements("tableConstructor") = "com.tang.intellij.lua.psi.LuaIndentRange"
  implements("forAStat|forBStat") = "com.tang.intellij.lua.psi.LuaIndentRange, com.tang.intellij.lua.psi.LuaParametersOwner, com.tang.intellij.lua.psi.LuaCommentOwner"
  implements("localDef|assignStat") = "com.tang.intellij.lua.psi.LuaDeclaration"
  implements("globalFuncDef|localFuncDef") = "com.tang.intellij.lua.psi.LuaFuncBodyOwner, com.tang.intellij.lua.psi.LuaDeclaration"
  implements("lambdaFuncDef") = "com.tang.intellij.lua.psi.LuaFuncBodyOwner"
  implements("expr|funcCall") = "com.tang.intellij.lua.psi.LuaExpression"
  implements("doStat|whileStat|repeatStat|ifStat|funcBody") = "com.tang.intellij.lua.psi.LuaIndentRange"
  extends(".*Expr") = expr
}

luaFile ::= stat_or_comment*

private comment ::= SHORT_COMMENT | DOC_COMMENT | BLOCK_COMMENT
private stat_or_comment ::= comment | stat ';'?
private chunk ::= stat_or_comment* (lastStat ';'?)?

private lazy_block ::= <<lazyBlock>>
block ::= chunk

private stat ::=
    funcCall |
    assignStat |
    doStat |
    whileStat |
    repeatStat |
    ifStat |
    forAStat |
    forBStat |
    classMethodDef |
    globalFuncDef |
    localFuncDef |
    localDef |
    lastStat

assignStat ::= varList '=' exprList { pin = 1 methods = [getComment] }
doStat ::= 'do' lazy_block 'end' { pin = 1 }
whileStat ::= 'while' expr 'do' lazy_block 'end' { pin = 1 }
repeatStat ::= 'repeat' lazy_block 'until' expr { pin = 1 }
ifStat ::= 'if' expr 'then' lazy_block ('elseif' expr 'then' lazy_block)* ('else' lazy_block)? 'end' { pin = 1 }
forAStat ::= 'for' paramNameDef '=' expr ',' expr (',' expr)? 'do' lazy_block 'end' { pin = 3 methods = [getComment getParamNameDefList] }
forBStat ::= 'for' parList 'in' exprList 'do' lazy_block 'end' { pin = 1 methods = [getComment] }

globalFuncDef ::= 'function' globalFuncNameDef funcBody {
    pin = 1
    methods = [getComment getName getPresentation getParamNameDefList]
    stubClass = "com.tang.intellij.lua.stubs.LuaGlobalFuncStub"
}

private checkFuncPrefix ::= ID ('.' | ':') ID

left funcPrefixRef ::= '.' <<repeat checkFuncPrefix 1>> nameRef
classMethodName ::=  nameRef (funcPrefixRef)* (('.' classFuncNameDef) | (':' classFuncNameDef)) { pin = 3 }
classMethodDef ::= 'function' classMethodName funcBody {
    pin = 2
    implements = [
        "com.tang.intellij.lua.psi.LuaFuncBodyOwner"
        "com.tang.intellij.lua.psi.LuaDeclaration"
        "com.tang.intellij.lua.psi.LuaClassMember"
        "com.tang.intellij.lua.psi.LuaFunction"
    ]
    stubClass = "com.tang.intellij.lua.stubs.LuaClassMethodStub"
    methods = [getComment getClassType getClassName getMethodName getParamNameDefList]
}

localFuncDef ::= 'local' 'function' localFuncNameDef funcBody {
    pin = 2
    methods = [getComment getParamNameDefList]
}
localDef ::= 'local' nameList ('=' exprList)? {
    pin = 1
    methods = [getComment]
}

lastStat ::= return exprList? | break

varList ::= var (',' var)*

nameList ::= nameDef (',' nameDef)*

exprList ::= (expr ',')* expr { methods = [guessTypeAt] }

valueExpr ::= nil | false | true | NUMBER | STRING | "..." | lambdaFuncDef | tableConstructor | funcCall | var | '(' expr ')'

expr ::= unopExpr | binopExpr | valueExpr { mixin = "com.tang.intellij.lua.psi.impl.LuaExpressionImpl" }

unopExpr ::= unop expr { pin = 1 }
binopExpr ::= valueExpr binop expr { pin = 2 }

private prefixExpr ::= '(' expr ')' | nameRef

private checkIndexExpr ::= '[' expr ']' | '.' ID { pin(".*") = 1 }

left indexExpr ::= '[' expr ']' | '.' ID {
    pin(".*") = 1
    methods = [guessPrefixType]
}

private check_args ::= STRING | <<fastCheckArgs>> | <<fastCheckTable>>
private checkCallExpr ::= check_args | ':' ID check_args { pin(".*") = 1 }

left callExpr ::= args | ':' ID args { pin(".*") = 1 methods = [guessPrefixType resolveFuncBodyOwner] }

private suffixIndexExpr ::= callExpr | <<repeat checkIndexExpr 2>> indexExpr
// 最终产生 index expr | nameRef
var ::= prefixExpr (suffixIndexExpr)* indexExpr | nameRef

private suffixCallExpr ::= <<repeat checkCallExpr 2>> callExpr | indexExpr
// 最终产生 callExpr
private funcCall ::= (prefixExpr (suffixCallExpr)*) callExpr

args ::=  '(' (exprList)? ')' | tableConstructor | STRING {
    pin(".*") = 1
}

lambdaFuncDef ::= 'function' funcBody {
    pin = 1
    methods = [getParamNameDefList]
}

funcBody ::= '(' (parList)? ')' lazy_block? 'end' { pin = 3 }

private parList ::= paramNameDef (',' paramNameDef)* (',' '...')? | '...'

tableConstructor ::= '{' (fieldList)? '}' {
    pin = 1
    methods = [findField]
}

fieldList ::= field (fieldSep field)* (fieldSep)?

field ::= '[' expr ']' '=' expr | nameDef '=' expr | expr

fieldSep ::= ',' | ';'

binop ::= '+' | '-' | '*' | '/' | '^' | '%' | '..' |
     '<' | '<=' | '>' | '>=' | '==' | '~=' |
     'and' | 'or'

unop ::= '-' | 'not' | '#'

fake name ::= ID {
    implements = [
        "com.tang.intellij.lua.psi.LuaNamedElement"
    ]
    methods = [setName getName]
}
fake funcNameDef ::= ID {
    extends = name
}
//全局函数专用名
globalFuncNameDef ::= ID {
    extends = funcNameDef
}
//local函数专用名
localFuncNameDef ::= ID {
    extends = funcNameDef
}
//xx:func专用名
classFuncNameDef ::= ID {
    extends = funcNameDef
}
nameDef ::= ID {
    implements = ["com.tang.intellij.lua.psi.LuaTypeResolvable" "com.intellij.psi.PsiNameIdentifierOwner"]
    methods = [resolveType getNameIdentifier]
    extends = name
}
nameRef ::= ID | SELF {
    implements = "com.tang.intellij.lua.psi.LuaNamedElement"
    methods = [setName getName getReference resolve resolveType]
}
paramNameDef ::= ID | SELF {
    extends = nameDef
}