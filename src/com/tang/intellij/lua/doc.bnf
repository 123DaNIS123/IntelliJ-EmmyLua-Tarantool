{
  parserClass="com.tang.intellij.lua.doc.parser.LuaDocParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"
  implements="com.tang.intellij.lua.doc.psi.LuaDocPsiElement"

  psiClassPrefix="LuaDoc"
  psiImplClassSuffix="Impl"
  psiPackage="com.tang.intellij.lua.doc.psi"
  psiImplPackage="com.tang.intellij.lua.doc.psi.impl"
  psiImplUtilClass="com.tang.intellij.lua.doc.psi.LuaDocPsiImplUtil"

  parserUtilClass="com.tang.intellij.lua.psi.LuaParserUtil"
  elementTypeHolderClass="com.tang.intellij.lua.doc.psi.LuaDocTypes"
  elementTypeClass="com.tang.intellij.lua.doc.psi.LuaDocElementType"
  elementTypeFactory="com.tang.intellij.lua.lang.LuaParserDefinition.createDocType"
  tokenTypeClass="com.tang.intellij.lua.doc.psi.LuaDocTokenType"

  tokens = [
  ID = 'ID'
  AT = '@'
  SHARP = '#'
  EQ = '='
  COMMA = ','
  TAG_NAME = 'TAG_NAME'
  TAG_PARAM = 'param'
  TAG_RETURN = 'return'
  PRIVATE = 'private'
  PUBLIC = 'public'
  CLASS = 'class'
  EXTENDS = "extends"
  INTERFACE = 'interface'
  FIELD = 'field'
  DEFINE = 'define'
  DASHES = 'DASHES'
  STRING_BEGIN = 'STRING_BEGIN'
  STRING = 'STRING'
  TYPE = 'type'
  ]
}

doc ::= (DASHES after_dash?)*

private after_dash ::= doc_item | STRING //{ recoverWhile=after_dash_recover }
//private after_dash_recover ::= !(DASHES)
private doc_item ::= '@' (global_def | param_def | return_def | tag_def | class_def | field_def | type_def) comment_string?

type_set ::= '#' class_name_ref (',' class_name_ref)*

//--- define
global_def ::= DEFINE {
    stubClass = "com.tang.intellij.lua.psi.stub.LuaGlobalFieldStub"
    implements = "com.tang.intellij.lua.psi.LuaTypeResolvable, com.tang.intellij.lua.doc.psi.LuaDocPsiElement"
    methods = [resolveType]
}

//--- field def
field_def ::= FIELD type_set { pin = 1 }

//--- tag
tag_value ::= ID
tag_def ::= TAG_NAME tag_value { pin = 1 }

//@param PARAM_NAME CLASS_REF
param_name_ref ::= ID {
    methods = [getReference]
}
param_def ::= TAG_PARAM param_name_ref type_set {
    pin = 1
    methods = [resolveType]
}

//--- return
type_list ::= type_set+
return_def ::= TAG_RETURN type_list {
    pin = 1
    methods = [resolveTypeAt]
}

//@class NAME extends CLASS_REF
class_name ::= ID {
    implements = "com.tang.intellij.lua.doc.psi.LuaDocNamedElement"
    methods = [getName setName]
}
class_name_ref ::= ID {
    methods = [getReference]
}
class_def ::= CLASS class_name (EXTENDS class_name_ref)?  {
    pin = 2
    stubClass = "com.tang.intellij.lua.psi.stub.LuaClassDefStub"
    methods = [getClassNameText]
}

// @type CLASS_REF
type_def ::= TYPE type_set { pin = 1 methods = [guessType] }

comment_string ::= STRING_BEGIN STRING? { pin = 1 }