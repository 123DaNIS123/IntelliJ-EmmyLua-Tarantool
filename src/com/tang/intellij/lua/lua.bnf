{
  parserClass="com.tang.intellij.lua.parser.LuaParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Lua"
  psiImplClassSuffix="Impl"
  psiPackage="com.tang.intellij.lua.psi"
  psiImplPackage="com.tang.intellij.lua.psi.impl"

  elementTypeHolderClass="com.tang.intellij.lua.psi.LuaTypes"
  elementTypeClass="com.tang.intellij.lua.psi.LuaElementType"
  tokenTypeClass="com.tang.intellij.lua.psi.LuaTokenType"

  tokens = [
        /* Keywords */
        AND = "and"
        BREAK = "break"
        DO = "do"
        ELSE = "else"
        ELSEIF = "elseif"
        END = "end"
        FALSE = "false"
        FOR = "for"
        FUNCTION = "function"
        IF = "if"
        IN = "in"
        LOCAL = "local"
        NIL = "nil"
        NOT = "not"
        OR = "or"
        REPEAT = "repeat"
        RETURN = "return"
        THEN = "then"
        TRUE = "true"
        UNTIL = "until"
        WHILE = "while"

        SHEBANG = "#!"
        ELLIPSIS = "..."
        CONCAT = ".."
        EQ = "=="
        GE = ">="
        LE = "<="
        NE = "~="
        MINUS = "-"
        PLUS = "+"
        MULT = "*"
        MOD = "%"
        DIV = "/"
        ASSIGN = "="
        GT = ">"
        LT = "<"
        LPAREN = "("
        RPAREN = ")"
        LBRACK = "["
        RBRACK = "]"
        LCURLY = "{"
        RCURLY = "}"
        GETN = "#"
        COMMA = ","
        SEMI = ";"
        COLON = ":"
        DOT = "."
        EXP = "^"

        SHORTCOMMENT="regexp:--.*"
        LUADOC_COMMENT="regexp:(---.*\n?)+"
        ID="regexp:[A-Za-z_][A-Za-z0-9_]*"
        NUMBER="regexp:-?([0-9]+|(0x[a-fA-F0-9]+))"
        STRING = "STRING"
  ]
}

luaFile ::= (SHORTCOMMENT | LUADOC_COMMENT | stat)*

chunk ::= (stat ';'?)* (lastStat ';'?)?

block ::= chunk

stat ::=
    assignStat |
    funcCall |
    doStat |
    whileStat |
    repeatStat |
    ifStat |
    forAStat |
    forBStat |
    globalFuncDef |
    localFuncDef |
    localDef

assignStat ::= varList '=' exprList { pin = 2 }
doStat ::= 'do' block 'end' { pin = 1 }
whileStat ::= 'while' expr 'do' block 'end' { pin = 1 }
repeatStat ::= 'repeat' block 'until' expr { pin = 1 }
ifStat ::= 'if' expr 'then' block ('elseif' expr 'then' block)* ('else' block)? 'end' { pin = 1 }
forAStat ::= 'for' ID '=' expr ',' expr (',' expr)? 'do' block 'end' { pin = 2 }
forBStat ::= 'for' nameList 'in' exprList 'do' block 'end' { pin = 2 }
globalFuncDef ::= 'function' funcName funcBody { pin = 1 }
localFuncDef ::= 'local' 'function' ID funcBody { pin = 2 }
localDef ::= 'local' nameList ('=' exprList)? { pin = 2 }

lastStat ::= return exprList? | break

funcName ::= ID ('.' ID)* (':' ID)?

varList ::= var (',' var)*

nameList ::= ID (',' ID)*

exprList ::= (expr ',')* expr

value ::= nil | false | true | NUMBER | STRING | "..." | func | tableConstructor | funcCall | var | '(' expr ')'

expr ::= unop expr | value (binop expr)?

prefixExpr ::= '(' expr ')' | ID

indexExpr ::= '[' expr ']' | '.' ID

callExpr ::= args | ':' ID args

suffixExpr ::= callExpr | indexExpr

var ::= prefixExpr (suffixExpr)* indexExpr | ID

funcCall ::= prefixExpr (suffixExpr)* callExpr

args ::=  '(' (exprList)? ')' | tableConstructor | STRING

func ::= 'function' funcBody

funcBody ::= '(' (parList)? ')' block? 'end'

parList ::= nameList (',' '...')? | '...'

tableConstructor ::= '{' (fieldlist)? '}'

fieldlist ::= field (fieldsep field)* (fieldsep)?

field ::= '[' expr ']' '=' expr | ID '=' expr | expr

fieldsep ::= ',' | ';'

binop ::= '+' | '-' | '*' | '/' | '^' | '%' | '..' |
     '<' | '<=' | '>' | '>=' | '==' | '~=' |
     'and' | 'or'

unop ::= '-' | 'not' | '#'